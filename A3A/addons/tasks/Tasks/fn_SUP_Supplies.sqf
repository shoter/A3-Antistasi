
#include "..\script_component.hpp"
FIX_LINE_NUMBERS()

// only public vars:
// state - key of state function
// checkpoint - key of checkpoint function (retrieves data for save)
// interval - time until next update in seconds
// lastUpdate - time previous update was run (from time command)

// init call:
// params ["_task", "_params", "_checkpoint"];
// every other call: _this = task


private _fnc_createBox = {
	params ["_pos"];
	private _box = "Land_FoodSacks_01_cargo_brown_F" createVehicle _pos;
	_box enableRopeAttach true;
	_box allowDamage false;
	[_box] call A3A_Logistics_fnc_addLoadAction;
	[_box, teamPlayer] call A3A_fnc_AIVEHinit;		// probably not useful?
	[_box,"Supply Box"] spawn A3A_fnc_inmuneConvoy;
	_task set ["_box", _box];
};

private _fnc_createTask = {
	private _nameDest = [_this get "_marker"] call A3A_fnc_localizar;
	private _displayTime = [((_this get "_endTime") - time) / 60] call FUNC(minutesFromNow);
	private _holdTime = (_this get "_difficulty") * 2;

	private _taskName = localize "STR_A3A_Tasks_LOG_Supplies_title";
	private _taskDesc = format [localize "STR_A3A_Tasks_LOG_Supplies_description", _nameDest, _displayTime, _holdTime];
	private _taskPos = markerPos (_this get "_marker");
	private _notify = isNil {_this get "checkpoint"};
	private _taskId = call FUNC(genTaskUID);
	[true, _taskId, [_taskDesc,_taskName], _taskPos, false, -1, _notify, "Heal", true] call BIS_fnc_taskCreate;
	_this set ["_taskId", _taskId];
};


params ["_params", "_checkpoint"];

private _task = createHashMap;

if (isNil "_checkpoint") then {
	_params params ["_marker"];

	// Specify larger object to leave space around box
	private _hqpos = getMarkerPos respawnTeamPlayer;
	private _boxPos = _hqPos findEmptyPosition [1,75,"C_Van_01_box_F"];
	if (_boxPos isEqualTo []) then { _boxPos = _hqPos findEmptyPosition [1,75,"Land_FoodSacks_01_cargo_brown_F"] };
	if (_boxPos isEqualTo []) then { _boxPos = _hqPos getPos [75 * sqrt random 1, random 360] };
	[_boxPos] call _fnc_createBox;

	// Determine end time and description
	private _difficulty = 1; //[1, 2] select (random 10 < tierWar);
	if (sidesX getVariable _marker == teamPlayer) then { _difficulty = 1 };
	
	_task set ["_marker", _marker];
	_task set ["_difficulty", _difficulty];
	_task set ["_endTime", time + 60 * (15 + 30*_difficulty)];
	_task call _fnc_createTask;
}
else {
	_params params ["_marker", "_boxPos", "_remTime", "_difficulty"];

	[_boxPos] call _fnc_createBox;

	_task set ["_marker", _marker];
	_task set ["_difficulty", _difficulty];
	_task set ["_endTime", time + _remTime];
	_task call _fnc_createTask;
};

[_task get "_taskId", "SUPP", "CREATED"] remoteExecCall ["A3A_fnc_taskUpdate", 2];

_task set ["checkpoint", "c_started"];
_task set ["state", "s_waitForPlace"];
_task set ["interval", 1];

_task set ["_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_title"];

_task set ["c_started", {
	[_this get "_marker", getPosATL (_this get "_box"), (_this get "_endTime") - time, _this get "_difficulty"];
}];


/////////////////////
// State functions //
/////////////////////

// called with local vars _box and _marker
_task set ["_fnc_placedCondition", {
	if (_box distance2d markerPos _marker > 40) exitWith {false};
	if (spawner getVariable _marker != 0) exitWith { false };
	if ((!isNull attachedTo _box) or (!isNull ropeAttachedTo _box)) exitWith { false };
	true;
}];

_task set ["s_waitForPlace",
{
	if (_this get "_endTime" < time) exitWith { _this set ["state", "s_failed"]; false };

	// check if box has been (initially) placed
	private _box = _this get "_box";
	private _marker = _this get "_marker";
	if !(call (_this get "_fnc_placedCondition")) exitWith {false};

	// Ok, now we go into the placed state
	_this set ["state", "s_boxPlaced"];
	_this set ["_countdown", 120 * (_this get "_difficulty")];			// maybe shouldn't reset?
	_this set ["_blockTime", time];

	// Remove captive from friendlies
	{
		if (captive _x) then { [_x, false] remoteExec ["setCaptive", _x] };
	} forEach (units teamPlayer inAreaArray [getPosATL _box, 300, 300]);

	// Pick random nearby player to send enemies towards
	private _player = allPlayers inAreaArray [getPosATL _box, 20, 20] select 0;
	if (!isNil "_player") then {
		["enemyInfo", [_marker, "mission", _player, 1.5, 3]] call A3A_fnc_garrisonOp;
	};
	false;

	// Difficult version: Send a QRF instead of longer time?
}];

_task set ["s_boxPlaced",
{
	if (_this get "_endTime" < time) exitWith { _this set ["state", "s_failed"]; false };

	// Check if box has been picked up or deserted
	private _box = _this get "_box";
	private _marker = _this get "_marker";
	if !(call (_this get "_fnc_placedCondition")) exitWith { _this set ["state", "s_waitForPlace"]; false };

	// Check that there are friendlies nearby and enemies not nearby
	private _enemyUnits = units Occupants + units Invaders;
	if ({_x call A3A_fnc_canFight} count (_enemyUnits inAreaArray [getPosATL _box, 50, 50]) > 0
		or {_x call A3A_fnc_canFight} count (units teamPlayer inAreaArray [getPosATL _box, 50, 50]) == 0) exitWith {
		if (time - (_this getOrDefault ["_blockTime", -30]) > 30) then {
			[_this get "_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_condition", getPosATL _box, 50] call FUNC(hintNear);
			_this set ["_blockTime", time];
		};
		false;
	};

	// Safe delivery success if there are no enemies anywhere near
	if (_enemyUnits inAreaArray [getPosATL _box, 500, 500] isEqualTo []) exitWith {
		[_this get "_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_deliveredSafe", getPosATL _box, 300] call FUNC(hintNear);
		_this set ["state", "s_succeeded"];	false;
	};

	// Need to know actual time since the previous update
	private _timeDiff = time - (_this get "lastUpdate");
	private _countdown = (_this get "_countdown") - _timeDiff;
	_this set ["_countdown", _countdown];

	// Delivery completion success
	if (_countdown <= 0) exitWith {
		[_this get "_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_delivered", getPosATL _box, 300] call FUNC(hintNear);
		_this set ["state", "s_succeeded"];	false;
	};

	// Show the countdown if blocked on last check
	if ("_blockTime" in _this) then {
		private _nearPlayers = call A3A_fnc_playableUnits inAreaArray [getPosATL _box, 300, 300];
		private _endTime = serverTime + _countdown;
		[_this get "_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_countdown", _endTime, _box, 50] remoteExec ["A3A_fnc_customHintCountdown", _nearPlayers];
		_this deleteAt "_blockTime";
	};
	false;
}];

_task set ["s_succeeded", {
	private _bonus = _this get "_difficulty";
	private _marker = _this get "_marker";

	[20 * _bonus, false, markerPos _marker, 250] call FUNC(rewardPlayers);     // any players within 250m
	[15 * _bonus, _marker] remoteExecCall ["A3A_fnc_citySupportChange", 2];
	[0, 200 * _bonus] remoteExec ["A3A_fnc_resourcesFIA", 2];

	[_this get "_taskId", "SUCCEEDED"] call BIS_fnc_taskSetState;
	_this set ["state", "s_cleanup"]; false;
}];
_task set ["s_failed", {
	// Need a message here just to avoid the cooldown?
	private _box = _this get "_box";
	[_this get "_hintTitle", localize "STR_A3A_Tasks_LOG_Supplies_failed", getPosATL _box, 300] call FUNC(hintNear);

	[-10, theBoss] call A3A_fnc_playerScoreAdd;

	[_this get "_taskId", "FAILED"] call BIS_fnc_taskSetState;
	_this set ["state", "s_cleanup"]; false;
}];
_task set ["s_cleanup", {
	// just dismantle anything remaining

	private _box = _this get "_box";
	private _ecpos = getposATL _box;
	deleteVehicle _box;
	private _emptybox = "Land_Pallet_F" createVehicle _ecpos;
	[_emptybox] spawn A3A_fnc_postmortem;

	// TODO: task deletion, rate throttling?
	// maybe we should restrict tasks based on request time not completion/failure time?
	// so then throttling moves to the request management

	// reverse-engineer task start time
	private _startTime = (_this get "_endTime") - 60 * (15 + 30*(_this get "_difficulty"));
	private _clearTime = 120 max ((_startTime + 1200) - time);
	Trace_3("Start time %1; time %2; clearTime %3", _startTime, time, _clearTime);
	[_clearTime, _this get "_taskId"] spawn {
		params ["_delay", "_taskId"];
		sleep _delay;
		[_taskId, "SUPP", "DELETED"] remoteExecCall ["A3A_fnc_taskUpdate", 2];
		[_taskId, true, true] call BIS_fnc_deleteTask;
	};

	true;		// delete the damned task
}];

// Return the task hashmap
_task;
