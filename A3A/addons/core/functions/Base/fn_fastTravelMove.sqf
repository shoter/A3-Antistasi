/*
Maintainer: Tiny-DM
    Moves this player or target group to the destination.
    This function manages hints, deducts money, finds the cost, manages the black screen, and moves the player.
    No checks are ran to ensure the player is actually able to fast travel to the given destination.

Arguments:
    <GROUP or OBJECT> HC group or player to fast travel
    <STRING or POSITION> Marker or position to travel to
    <OBJECT> Player who ordered the fast travel
    <BOOL> Skip the money cost [DEFAULT = false]
    <SCALAR> Travel time divisor, e.g. 3 for three times faster [DEFAULT = 1]
    <STRING> Player statistic counted for the ordering player on arrival, "fastTravels" or "flagTeleports" [DEFAULT = "fastTravels"]

Scope: Local
Environment: Scheduled
Public: No
Dependencies:

Example:
    [player,"Synd_HQ",player] spawn A3A_fnc_fastTravelMove; // Moves player back to HQ
    [_hcGroup,"airport",player] spawn A3A_fnc_fastTravelMove; // Moves given HC group to the first defined airport, regardless of side.
    [player, getPosATL A3A_deployedFlag, player, true, 3] spawn A3A_fnc_fastTravelMove; // Moves player to the rally flag, free and three times faster

License: APL-ND
*/

#include "..\..\script_component.hpp"
FIX_LINE_NUMBERS()

params ["_groupX", "_base", "_player", ["_free", false, [false]], ["_timeDivisor", 1, [0]], ["_statKey", "fastTravels", [""]]];
private _titleStr = localize "STR_A3A_fn_dialogs_ftradio_title";
private _isHC = (_groupX isEqualType grpNull);
private _destCentre = if (_base isEqualType "") then { markerPos _base } else { _base };

private _ftUnit = [_player, leader _groupX] select _isHC;
[_ftUnit, [vehicle _ftUnit], _destCentre] call FUNCMAIN(calculateFastTravelCost) params ["_travelCost","_travelTime"];
_travelTime = (round (_travelTime / _timeDivisor)) max 1;
if (!_isHC and !_free) then {[-_travelCost] call A3A_fnc_resourcesPlayer};

if (!_isHC) then {
	openMap false;
	disableUserInput true;
	cutText [format [localize "STR_A3A_fn_dialogs_fastTravelRadio_begin", ([[_travelTime] call A3A_fnc_secondsToTimeSpan,0,0,false,2] call A3A_fnc_timeSpan_format)],"BLACK",1];
	while {_travelTime >= 1} do {
        sleep 1;
		_travelTime = _travelTime - 1;
		cutText [format [localize "STR_A3A_fn_dialogs_fastTravelRadio_begin", ([[_travelTime] call A3A_fnc_secondsToTimeSpan,0,0,false,2] call A3A_fnc_timeSpan_format)],"BLACK",0.001];
	};
} else {
	[_titleStr, format [localize "STR_A3A_fn_dialogs_ftradio_grp_moving", groupID _groupX]] call A3A_fnc_customHint;
	sleep _travelTime;
};

// Any reason to cancel at this point?
// "got into vehicle" would be one? But handled by the selection code...
//if (_checkForPlayer and ((_base != "SYND_HQ") and !(_base in airportsX)))
//exitWith {[_titleStr, format [localize "STR_A3A_fn_dialogs_ftradio_cancelled",groupID _groupX]] call A3A_fnc_customHint;};

// So, the rules:
// If it's an HC group, the group and all its vehicles (including occupied statics) will be teleported
// If you're a driver, your vehicle will be teleported
// If you're not in a vehicle, you will be teleported personally
// If you're group lead and have squad AI within 50m, those (and their vehicles, if driver) will be teleported

// Find vehicles & units to be fast-travelled
private _ftVehicles = [];
private _ftUnits = call {
	if (_isHC) exitWith { units _groupX };
	if (player != leader group player) exitWith { [player] };
	(units group player inAreaArray [getPosATL player, 50, 50] select { !isPlayer _x }) + [player];
};
if (_isHC) then { _ftVehicles pushBackUnique (assignedVehicles _groupX # 0) };
{
	private _veh = vehicle _x;
	if (_isHC and _veh isKindOf "StaticWeapon") then { _ftVehicles pushBackUnique _veh; continue };
	if (_x == _veh or _x != driver _veh) then { continue };
	if (_veh isKindOf "Land" and canMove _veh) then { _ftVehicles pushBackUnique _veh };
} forEach _ftUnits;
_ftUnits = _ftUnits select { _x == vehicle _x };


// Do the actual teleport. Vehicles before units, try to place everything in proximity.
private _destPos = _destCentre getPos [30, random 360];
{
	private _vehPlace = false;
	if !(_x isKindOf "StaticWeapon") then {
		// attempt to place on a road section
		private _emptyRoads = _destPos nearRoads 20 select { _x nearEntities 10 isEqualTo [] };
		if (_emptyRoads isEqualTo []) then { _emptyRoads = _destPos nearRoads 50 select { _x nearEntities 10 isEqualTo [] } };
		if (_emptyRoads isEqualTo []) then { _emptyRoads = _destPos nearRoads 100 select { _x nearEntities 10 isEqualTo [] } };
		if (_emptyRoads isNotEqualTo []) then {
			private _road = selectRandom _emptyRoads;
			private _pos1 = getRoadInfo _road # 6;
			private _pos2 = getRoadInfo _road # 7;
			private _dir = if (_pos1 distance2d _destPos < _pos2 distance2d _destPos) then { _pos2 getDir _pos1 } else { _pos1 getDir _pos2 };
			_vehPlace = [getPosATL _road, _dir];
		};
	};
	if (_vehPlace isEqualType false) then {
		// go find a random empty position instead
		private _testDir = random 360;
		private _pos = [_destPos, _x, _testDir, 5, 20, 20] call A3A_fnc_findEmptyPosCar;
		if (_pos isNotEqualTo []) exitWith { _vehPlace = [_pos, _testDir] };
		_pos = [_destPos, _x, _testDir, 20, 30, 30] call A3A_fnc_findEmptyPosCar;
		if (_pos isNotEqualTo []) exitWith { _vehPlace = [_pos, _testDir] };
		_pos = [_destPos, _x, _testDir, 50, 100, 50] call A3A_fnc_findEmptyPosCar;
		if (_pos isNotEqualTo []) exitWith { _vehPlace = [_pos, _testDir] };
	};
	if (_vehPlace isEqualType false) then {
		[_titleStr, localize "STR_A3A_fn_dialogs_ftradio_fail"] call A3A_fnc_customHint;
		continue;
	};
	isNil {
		// Actually move vehicle
		_x setPosATL _vehPlace#0;
		_x setDir _vehPlace#1;
		_x allowDamage false;
	};
	// Update destPos to last non-static vehicle
	if !(_x isKindOf "StaticWeapon") then { _destPos = _vehPlace # 0 }; 

} forEach _ftVehicles;

{
	private _unitPos = _destPos findEmptyPosition [5,50,typeOf _x];
	if (_unitPos isEqualTo []) then { _unitPos = _destPos getPos [5 + 45 * sqrt random 1, random 360] };		// whatever, units are pretty safe
	_x setPosATL _unitPos;
	if (!isPlayer _x and !(_x getVariable ["incapacitated",false])) then {
		_x setVariable ["rearming",false];
		_x doWatch objNull;
		_x doFollow leader _x;
	};
} forEach _ftUnits;

// Player statistics: the move went through
[[_player] call A3A_fnc_playerStats_getUID, [[_statKey, 1]]] remoteExecCall ["A3A_fnc_playerStats_add", 2];

if (!_isHC) then {
	disableUserInput false;
	cutText [localize "STR_A3A_fn_dialogs_fastTravelRadio_end","BLACK IN",1]
} else {
	[_titleStr, format [localize "STR_A3A_fn_dialogs_ftradio_grp_arrived", groupID _groupX]] call A3A_fnc_customHint;
};
[] call A3A_fnc_playerLeashRefresh;

sleep 5;
{ _x allowDamage true } forEach _ftVehicles;
